group 'org.ice1000'
version '1.0'

apply plugin: 'java'
apply plugin: 'application'

buildscript {
	ext.kotlin_version = '1.2.60'
	ext.frice_version = '1.8.5'
	ext.lice_version = '3.3.2'
	repositories { mavenCentral() }
}

mainClassName = 'org.frice.th.Touhou'

def useLocalFriceEngine = false

repositories {
	mavenCentral()
	if (useLocalFriceEngine) flatDir { dirs '../FriceEngine/build/libs' }
}

sourceSets {
	main.java.srcDirs = ['src']
	test.java.srcDirs = ['test']
	main.resources.srcDirs = ['res']
}

configurations { library }

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
	options.setDeprecation true
}

repositories {
	jcenter()
	maven { url 'https://dl.bintray.com/ice1000/Lice' }
}

dependencies {
	compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
	compile group: 'org.jetbrains', name: 'annotations', version: '15.0'

	if (useLocalFriceEngine) library name: "engine-$frice_version"
	else library "org.frice:engine:$frice_version"

	library "org.lice:lice:$lice_version"

	configurations.compile.extendsFrom configurations.library
	testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
	baseName = 'game'
	manifest { attributes 'Main-Class': mainClassName }
}

task fatJar(type: Jar) {
	baseName = 'game-exe'
	description 'Assembles a jar archive containing the main classes and all the dependencies.'
	group 'build'
	manifest { attributes 'Main-Class': mainClassName }
	from { configurations.library.collect { it.isDirectory() ? it : zipTree(it) } }
	with jar
}

artifacts {
	archives fatJar
	archives jar
}
